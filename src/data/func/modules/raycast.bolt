function func:raycast/cast:
    summon area_effect_cloud ~ ~ ~ {Duration:1, Tags:["ray"]}
    execute anchored eyes rotated as @s run tp @e[tag=ray,limit=1,sort=nearest] ^ ^ ^ ~ ~
    execute as @e[tag=ray,limit=1,sort=nearest] run function func:raycast/process

function func:raycast/process:
    scoreboard players set @s steps 50
    execute at @s run function func:raycast/move
    kill @s

function func:raycast/move:
    tp @s ^ ^ ^.2

    execute if block ~ ~ ~ white_bed:
        tag @s add hit
        execute align xyz positioned ~.5 ~ ~.5 run function func:logistics/splitter/spawn
    
    execute if block ~ ~ ~ lightning_rod:
        tag @s add hit
        execute align xyz positioned ~.5 ~ ~.5 run function func:logistics/inserter/spawn

    execute if block ~ ~ ~ beehive:
        tag @s add hit
        execute align xyz positioned ~.5 ~ ~.5 run function func:logistics/drill/spawn
    
    execute if block ~ ~ ~ furnace:
        tag @s add hit
        execute align xyz positioned ~.5 ~ ~.5 run function func:logistics/furnace/spawn
    
    # iterate
    scoreboard players remove @s steps 1
    execute as @s[tag=!hit,scores={steps=1..}] at @s run function func:raycast/move